\chapter{Primitive Recursive Functions}

\section{Introduction}
The definition of primitive recursive functions we use comes
from Russel O'Connors formalization in \coq{} of
G\"odel's incompleteness theorems~\cite{OConnor05}, now hosted in
the \texttt{theories/ordinals/Ackermann} directory.

This chapter contains some comments on Russel's library, as well as a few extensions.
Contributions (under the form of comments, new examples or exercises) are welcome!. 

\section{First look at the Ackermann Library}

O'Connor's library on Gödel's incompleteness theorems contains a little more 
than 45K lines of scripts. The part dedicated to primitive recursive functions and Peano arithmetics is 32K lines long and is originally structured in 38 modules.
Thus, we propose a slow exploration of this library, through examples and exercises. Our additions to the original library 
are stored in a sub-directory \texttt{Ackermann/MoreAck}.

\section{Basic Definitions}
\index{maths}{Primitive recursive functions}

The formal definition of primitive recursive functions lies in the library
\href{../theories/html/hydras.Ackermann.primRec.html}{Ackermann.primRec},
with preliminary definitions in 
\href{../theories/html/hydras.Ackermann.extEqualNat.html}{Ackermann.extEqualNat}
and
\href{../theories/html/hydras.Ackermann.misc.html}{Ackermann.misc}.

\subsection{Functions of arbitrary arity}

Library \texttt{primRec} allows us to consider primitive functions on \texttt{nat}, with any number of arguments, in 
curried form. This is made possible in 
\href{../theories/html/hydras.Ackermann.extEqualNat.html}{Ackermann.extEqualNat} by the following definition:

\begin{Coqsrc}
Fixpoint naryFunc (n : nat) : Set :=
  match n with
  | O => nat
  | S n => nat -> naryFunc n
  end.
\end{Coqsrc}

For instance (\texttt{naryFunc 1}) is convertible to \texttt{nat -> nat} and (\texttt{naryFunc 3})
to \texttt{nat -> nat -> nat -> nat}.

\vspace{4pt}
\noindent
\emph{From\href{../theories/html/hydras.Ackermann.MoreAck.PrimRecExamples.html}{Ackermann.MoreAck.PrimRecExamples}}.
\begin{Coqsrc}
Require Import primRec.
Import extEqualNat.

Compute naryFunc 3.
\end{Coqsrc}

\begin{Coqanswer}
= nat -> nat -> nat -> nat
  : Set  
\end{Coqanswer}

Thus, the function \texttt{plus} has also type (\texttt{naryFunc 2}).

Likewise, arbitrary boolean predicates may have an arbitrary number of arguments. The dependent type
(\texttt{naryRel $n$}), defined in the same way as \texttt{naryFunc}, is the type of $n$-ary functions from
\texttt{nat} into \texttt{bool}.

\begin{Coqsrc}
Compute naryRel 2.
\end{Coqsrc}

\begin{Coqanswer}
 = nat -> nat -> bool
     : Set
\end{Coqanswer}

The magic of dependent types makes it possible to define recursively extensional equality between functions of the same arity.

\index{coq}{Dependent types}
\index{coq}{Dependently typed functions}
\vspace{4pt}
\noindent
\emph{From \href{../theories/html/hydras.Ackermann.extEqualNat.html}{Ackermann.extEqualNat}}
\begin{Coqsrc}
Fixpoint  extEqual (n : nat) : forall  (a b : naryFunc n), Prop :=
  match n with
    0 => fun a b => a = b
  | S p => fun a b => forall c, extEqual p (a c) (b c)
  end.
\end{Coqsrc}

\begin{Coqsrc}
Compute extEqual 2.
\end{Coqsrc}

\begin{Coqanswer}
     = fun a b : naryFunc 2 => forall x x0 : nat, a x x0 = b x x0
     : naryFunc 2 -> naryFunc 2 -> Prop
 \end{Coqanswer}
 
\begin{Coqsrc}
Example extEqual_ex1 : extEqual 2 mult (fun x y =>  y * x + x - x) .
Proof.
  intros x y.
\end{Coqsrc}

\begin{Coqanswer}
  x, y : nat
  ============================
  extEqual 0 (x * y) (y * x)
\end{Coqanswer}

\begin{Coqsrc}
  cbn.
\end{Coqsrc}

\begin{Coqanswer}
1 subgoal (ID 10)
  
  x, y : nat
  ============================
  x * y = y * x + x - x
\end{Coqanswer}

\begin{Coqsrc}
  rewrite <- Nat.add_sub_assoc, Nat.sub_diag.
  - ring.
  - apply le_n.  
Qed.
\end{Coqsrc}

\subsection{A Data-type for Primitive Recursive Functions}

The traditional definition of the set of primitive recursive functions is structured as an inductive definition 
in five rules: three base cases, and two construction rules. Primitive recursive functions are total functions from $\mathbb{N}^n$ to
$\mathbb{N}$, for some $n\in\mathbb{N}$.

\begin{description}
  \item[zero] the constant function of value $0$ is primitive recursive.
\item[S] The successor function $S:\mathbb{N}\rightarrow\mathbb{N}$ is primitive recursive.
 \item[projections] For any pair $0< i\leq n$, the projection $\pi_{i,n}: \mathbb{N}^n\rightarrow\mathbb{N}$, defined by $\pi_{i,n}(x_1,x_2,\dots,x_{n})=x_i$, is primitive recursive.
\item[composition] For any $n$ and $m$, if $h: \mathbb{N}^m\rightarrow\mathbb{N}$, and
$g_0,\dots, g_{m-1}$ are primitive recursive of $n$ arguments, then the function which maps any
tuple $(x_0,\dots,x_{n-1})$ to $h(g_0(x0,\dots,x_{n-1}),\dots, g_{m-1}(x0,\dots,x_{n-1}))$ is primitive recursive.
\item[primitive recursion]
If $g: \mathbb{N}^n\rightarrow\mathbb{N}$ and $h: \mathbb{N}^{n+2}\rightarrow\mathbb{N}$ are primitive recursive, then the function from $\mathbb{N}^{n+1}$ into $\mathbb{N}$ defined by
\begin{align}
f(0,x_1,\dots,x_n)&=g(x_1,\dots,x_n)\\
f(S(p),x_1,\dots,x_n)&=h(p,f(p, x_1,\dots,x_n),  x_1,\dots,x_n)
\end{align} 
is primitive recursive.
\end{description}

O'Connor's formalization of primitive recursive functions takes the form of two mutually inductive dependent data types, each constructor of which is associated with one of these  rules.
These two types are (\texttt{PrimRec $n$}) (primitive recursive functions of $n$ arguments), and
(\texttt{PrimRecs $n$ $m$}) ($m$-tuples of primitive recursive functions of $n$ arguments).

\index{coq}{Dependent types}
\index{coq}{Mutually inductive types}

\label{def:Primrec}
\vspace{4pt}
\noindent
\emph{From \href{../theories/html/hydras.Ackermann.primRec.html}{Ackermann.primRec}.}
\begin{Coqsrc}
Inductive PrimRec : nat -> Set :=
  | succFunc : PrimRec 1
  | zeroFunc : PrimRec 0
  | projFunc : forall n m : nat, m < n -> PrimRec n
  | composeFunc :
      forall (n m : nat) (g : PrimRecs n m) (h : PrimRec m), PrimRec n
  | primRecFunc :
      forall (n : nat) (g : PrimRec n) (h : PrimRec (S (S n))), 
      PrimRec (S n)
with PrimRecs : nat -> nat -> Set :=
  | PRnil : forall n : nat, PrimRecs n 0
  | PRcons : forall n m : nat, PrimRec n -> PrimRecs n m -> 
                   PrimRecs n (S m).
\end{Coqsrc}

\begin{remark}
\label{projFunc-order-of-args}
Beware of the conventions used in the \texttt{primRec} library!
The constructor (\texttt{projFunc $n$ $m$})  is associated with the projection $\pi_{n-m,n}$ and \emph{not}
$\pi_{n, m}$.
For instance, the projection $\pi_{2,5}$ defined by $\pi_{2,5}(a,b,c,d,e)=b$ corresponds to the term
(\texttt{projFunc 5 3 H}), where \texttt{H} is a proof of $3<5$.
 This fact is reported in the comments of \texttt{primRec.v}. We presume that this convention makes it easier to define the evaluation function \texttt{evalProjFunc $n$} (see next sub-section) . Trying the other convention is left as an exercise.
\end{remark}



\subsection{A little bit of Semantics} 
Please note that inhabitants of type (\texttt{PrimRec $n$}) are not \coq{} functions like \texttt{Nat.mul}, or factorial, etc. The data-type (\texttt{PrimRec $n$}) is indeed an abstract syntax for the language of primitive recursive functions. The bridge between this language and the word of usual functions
is an interpretation function (\texttt{evalprimRec $n$})  of type
$\texttt{PrimRec}\,n \rightarrow  \texttt{naryFunc}\,n$ , together with the helper function 
(\texttt{evalprimRecS $n$ $m$}) of type 
$\texttt{PrimRecs}\,n\,m \rightarrow  \texttt{Vector.t}\,(\texttt{naryFunc}\,n)\,m$.

\index{coq}{Dependent pattern matching}
Both functions are mutually defined through dependent pattern matching. We advise the readers who 
would feel uneasy with dependent types to consult Adam Chlipala's \emph{cpdt}  book~\cite{chlipalacpdt2011}. We leave it to the reader  to look also at the helper functions in
\href{../theories/html/hydras.Ackermann.primRec.html}{Ackermann.primRec}.


\begin{Coqsrc}
Fixpoint evalPrimRec (n : nat) (f : PrimRec n) {struct f} : 
 naryFunc n :=
  match f in (PrimRec n) return (naryFunc n) with
  | succFunc => S
  | zeroFunc => 0
  | projFunc n m pf => evalProjFunc n m pf
  | composeFunc n m l f =>
      evalComposeFunc n m (evalPrimRecs _ _ l) (evalPrimRec _ f)
  | primRecFunc n g h =>
      evalPrimRecFunc n (evalPrimRec _ g) (evalPrimRec _ h)
  end
 
 with evalPrimRecs (n m : nat) (fs : PrimRecs n m) {struct fs} :
 Vector.t (naryFunc n) m :=
  match fs in (PrimRecs n m) return (Vector.t (naryFunc n) m) with
  | PRnil a => Vector.nil  (naryFunc a)
  | PRcons a b g gs =>
       Vector.cons _ (evalPrimRec _ g) _  (evalPrimRecs _ _ gs)
  end.
\end{Coqsrc}

Looks complicated? The following examples show that, when
the arity is fixed, these definitions behave well w.r.t. 
\coq's reduction rules. Moreover, they make the interpretation functions more ``concrete''.

\begin{Coqsrc}
Example Ex1 : evalPrimRec 0 zeroFunc = 0.
Proof. reflexivity. Qed.

Example Ex2 a : evalPrimRec 1 succFunc a = S a.
Proof. reflexivity. Qed.

Example Ex3 a b c d e f: forall (H: 2 < 6),
    evalPrimRec 6
                (projFunc 6 2 H) a b c d e f = d.
Proof. reflexivity. Qed.

Example Ex4 (x y z : PrimRec 2) (t: PrimRec 3):
  let u := composeFunc 2 3
                       (PRcons 2 2 x
                               (PRcons 2 1 y
                                       (PRcons 2 0 z
                                               (PRnil 2))))
                       t in
  let f := evalPrimRec 2 x in
  let g := evalPrimRec 2 y in
  let h := evalPrimRec 2 z in
  let i := evalPrimRec 3 t in
  let j := evalPrimRec 2 u in
  forall a b, j a b = i (f a b) (g a b) (h a b).
Proof. reflexivity. Qed.

Example Ex5 (x : PrimRec 2)(y: PrimRec 4):
  let g := evalPrimRec _ x in
  let h := evalPrimRec _ y in
  let f := evalPrimRec _ (primRecFunc _ x y) in
  forall a b,  f 0 a b = g a b.
Proof. reflexivity.   Qed.                          

Example Ex6 (x : PrimRec 2)(y: PrimRec 4):
  let g := evalPrimRec _ x in
  let h := evalPrimRec _ y in
  let f := evalPrimRec _ (primRecFunc _ x y) in
  forall n a b,  f (S n) a b = h n (f n a b) a b.
Proof. reflexivity.   Qed.                          
\end{Coqsrc}


Another example?
Let us consider the following term\footnote{Of course, we never typed this term \emph{verbatim}; we obtained it as the result of some computation we leave it the reader to guess and reproduce.}:

\label{sect:bigfac}
\begin{Coqsrc}
Example bigPR : PrimRec 1 :=
primRecFunc 0
  (composeFunc 0 1 (PRcons 0 0 zeroFunc (PRnil 0)) succFunc)
  (composeFunc 2 2
    (PRcons 2 1
      (composeFunc 2 1
         (PRcons 2 0 (projFunc 2 1 (le_n 2))
                 (PRnil 2))
         succFunc)
      (PRcons 2 0
        (composeFunc 2 1
          (PRcons 2 0
             (projFunc 2 0
                       (le_S 1 1 (le_n 1)))
             (PRnil 2))
          (projFunc 1 0 (le_n 1))) (PRnil 2)))
    (primRecFunc 1 (composeFunc 1 0 (PRnil 1) zeroFunc)
       (composeFunc 3 2
         (PRcons 3 1
            (projFunc 3 1 (le_S 2 2 (le_n 2)))
            (PRcons 3 0 (projFunc 3 0
                          (le_S 1 2
                                (le_S 1 1 (le_n 1))))
                    (PRnil 3)))
         (primRecFunc 1 (projFunc 1 0 (le_n 1))
                      (composeFunc 3 1
                          (PRcons 3 0
                                  (projFunc 3 1 (le_S 2 2 (le_n 2)))
                                  (PRnil 3))
                          succFunc))))). 
\end{Coqsrc}

Let us now interpret this term as an arithmetic function.

\begin{Coqsrc}
Example  mystery_fun : nat -> nat := evalPrimRec 1 bigPR.

Compute map mystery_fun[0;1;2;3;4;5;6] : t nat _.
\end{Coqsrc}

\begin{Coqanswer}
 = 1 :: 1 :: 2 :: 6 :: 24 :: 120 :: 720 :: nil
     : t nat 7
\end{Coqanswer}

Ok, the term \texttt{bigPR} looks to be a primitive recursive definition of the factorial function, although we haven't proved this fact yet. Fortunately, we will see in the following sections much simpler ways to prove that a given function is primitive recursive, whithout looking at an unreadable term.

\section{Proving that a given arithmetic function is primitive recursive}

The example in the preceding section clearly shows that, in order to prove that a given arithmetic function
(defined in \gallina{} as usual) is primitive recursive, trying to give  by hand a term  of type \texttt{PrimRec $n$} is not a good method, since such terms may be huge and complex, even for simple arithmetic functions. The method proposed in Library \texttt{primRec} is the following one:

\begin{enumerate}
\item Define a type corresponding to the statement "the function \texttt{$f$:naryFunc $n$} is primitive recursive ''.
\item Prove handy lemmas which may help to prove that a given function is primitive recursive.
\end{enumerate}

Thus, the proof that a function, like \texttt{factorial}, is primitive recursive may be interactive, whithout having to type complex terms at any step of the development.

\subsection{The predicate \texttt{isPR}}

Let $f$ be an arithmetic function of arity $n$. We say that $f$ is primitive recursive if $f$ is \textbf{extensionnaly}
equal to the interpretation of some term of type \texttt{PrimRec $n$}. 

\vspace{4pt}
\noindent
\emph{From \href{../theories/html/hydras.Ackermann.primRec.html}{Ackermann.primRec}.}
\begin{Coqsrc}
Definition isPR (n : nat) (f : naryFunc n) : Set :=
  {p : PrimRec n | extEqual n (evalPrimRec _ p) f}.  
\end{Coqsrc}

The library \texttt{primRec} contains a large catalogue of lemmas allowing to prove statements 
of the form (\texttt{isPR $n$ $f$}). We won't list all these lemmas here, but give a few examples of
how they may be applied.

\begin{remark}
In the library \texttt{primRec}, all these lemmas are opaque (registered with \texttt{Qed}. Thus they do not allow the user to look at the witness of a proof of a \texttt{isPR} statement. Our example of page\pageref{sect:bigfac} was built using a  copy of \texttt{primRec.v} where many \texttt{Qed}s have been replaced with
\texttt{Defined}s.
\end{remark}

\subsubsection{Elementary Proofs of \texttt{isPR} statements}

The constructors \texttt{zeroFunc}, \texttt{succFunc},  and \texttt{projFunc} of type
\texttt{PrimRec} allows us to write trivial proofs of primitive recursivity. 
Although  the following lemmas are alreday proven in 
\href{../theories/html/hydras.Ackermann.primRec.html}{Ackermann.primRec.v},
we wrote alternate proofs in 
\href{../theories/html/hydras.Ackermann.MoreAck.PrimRecExamples.html}%
{Ackermann.MoreAck.PrimRecExamples.v}, in order to illustrate the main proof patterns.

\begin{Coqsrc}
Module Alt.
  
Lemma zeroIsPR : isPR 0 0.
Proof.
  exists zeroFunc.
\end{Coqsrc}

\begin{Coqanswer}
1 subgoal (ID 90)
  
  ============================
  extEqual 0 (evalPrimRec 0 zeroFunc) 0
\end{Coqanswer}

\begin{Coqsrc}
  cbn.
\end{Coqsrc}


\begin{Coqanswer}
1 subgoal (ID 91)
  
  ============================
  0 = 0
\end{Coqanswer}

\begin{Coqsrc}
  reflexivity.
Qed.
\end{Coqsrc}


Likewise, we prove that the successor function on \texttt{nat} is primitive recursive too.

\begin{Coqsrc}
Lemma SuccIsPR : isPR 1 S.
Proof.
  exists succFunc; cbn; reflexivity.
Qed.
\end{Coqsrc}

Projections are proved primitive recursive, case by case (many examples in 
\href{../theories/html/hydras.Ackermann.primRec.html}{Ackermann.primRec.v}).
\emph{Please notice again that the name of the projection follows the mathematical tradition, 
whilst the arguments of  \texttt{projFunc} use another convention (\emph{cf} remark~\vref{projFunc-order-of-args}).}

\begin{Coqsrc}
Lemma pi2_5IsPR : isPR 5 (fun a b c d e => b).
Proof.
 assert (H: 3 < 5) by auto.
 exists (projFunc 5 3 H).
 cbn; reflexivity.
Qed.
\end{Coqsrc}

Please note that the projection $\pi_{1,1}$ is just the identity on \texttt{nat}, and is realized by 
(\texttt{projFunc 1 0}).


\vspace{4pt}
\noindent
\emph{From \href{../theories/html/hydras.Ackermann.primRec.html}{Ackermann.primRec}.}

\begin{Coqsrc}
Lemma idIsPR : isPR 1 (fun x : nat => x).
Proof.
  assert (H: 0 < 1) by auto.
  exists (projFunc 1 0 H); cbn; auto.
Qed.
\end{Coqsrc}

\subsubsection{Using Function Composition}

Let us look at the proof that any constant $n$ of type \texttt{nat} has type (\texttt{PR 0})
(lemma  \texttt{const1\_NIsPR} of \texttt{primRec}). We carry out a proof by induction on $n$, the base case of which is already proven.
Now, let us assume $n$ is \texttt{PR $n$}, with $x:\texttt{PrimRec}\,0$ as a ``realizer''.
Thus we would like to compose this constant function with the unary successor function.

This is exactly the role of the instance \texttt{composeFunc 0 1} of the dependently typed
function \texttt{composeFunc}, as shown by the following lemma.

\begin{Coqsrc}
Fact compose_01 :
    forall (x:PrimRec 0) (t : PrimRec 1),
    let c := evalPrimRec 0 x in
    let f := evalPrimRec 1 t in
    evalPrimRec 0 (composeFunc 0 1
                               (PRcons 0 0 x (PRnil 0))
                               t)  =
     f c.
Proof. reflexivity. Qed.
\end{Coqsrc}

Thus, we get a quite simple proof of \texttt{const1\_NIsPR}.


\vspace{4pt}
\noindent
\emph{From \href{../theories/html/hydras.Ackermann.MoreAck.PrimRecExamples.html}{Ackermann.MoreAck.PrimRecExamples}}.
\begin{Coqsrc}
Lemma  const1_NIsPR n : isPR 0 n. 
Proof.
  induction n.
  - apply zeroIsPR.
  - destruct IHn as [x Hx].
   exists (composeFunc 0 1 (PRcons 0 0 x (PRnil 0)) succFunc). 
   cbn in *; intros; now rewrite Hx.
Qed.
\end{Coqsrc}


\subsubsection{Proving that \texttt{plus} is primitive recursive}

The lemma \texttt{plusIsPR} is already proven in \href{../theories/html/hydras.Ackermann.primRec.html}{Ackermann.primRec}. We present in 
\href{../theories/html/hydras.Ackermann.MoreAck.PrimRecExamples.html}{Ackermann.MoreAck.PrimRecExamples}
a commented version of this proof, 

First, we look for lemmas which may help to prove that a given function obtained with the recursor \texttt{nat\_rec} is primitive recursive.

\begin{Coqsrc}
Search (is_PR 2 (fun _ _ => nat_rec _ _ _ _)).
\end{Coqsrc}

\begin{Coqanswer}
ind1ParamIsPR:
  forall f : nat -> nat -> nat -> nat,
  isPR 3 f ->
  forall g : nat -> nat,
  isPR 1 g ->
  isPR 2
    (fun a b : nat =>
     nat_rec (fun _ : nat => nat)
                 (g b) (fun x y : nat => f x y b) a)
\end{Coqanswer}

We prove that the library function \texttt{plus} is extensionally equal to a function defined with
\texttt{nat\_rec}.

\begin{Coqsrc}
Definition plus_alt x y  :=
              nat_rec  (fun n : nat => nat)
                       y
                       (fun z t =>  S t)
                       x.

Lemma plus_alt_ok:
  extEqual 2 plus_alt plus.
Proof.
  intro x; induction x; cbn; auto.
  intros y; cbn; now rewrite <- (IHx y).
Qed.
\end{Coqsrc}

A last lemma before the proof:

\begin{Coqsrc}
Lemma isPR_extEqual_trans n : forall f g, isPR n f ->
                                    extEqual n f g ->
                                    isPR n g.
Proof.
 intros f g [x Hx]; exists x.
 apply extEqualTrans with f; auto.
Qed.
\end{Coqsrc}

Let us start now.

\begin{Coqsrc}
Lemma plusIsPR : isPR 2 plus.
Proof.
  apply isPR_extEqual_trans with plus_alt.
  - unfold plus_alt; apply ind1ParamIsPR.
\end{Coqsrc}

\begin{Coqanswer}
2 subgoals (ID 126)
  
  ============================
  isPR 3 (fun _ y _ : nat => S y)

subgoal 2 (ID 127) is:
 isPR 1 (fun b : nat => b)
\end{Coqanswer}

We alreday proved that \texttt{S} is \texttt{PR 1}, but we need to consider a function of three arguments, which ignores its first and third arguments.
Fortunately, the library \texttt{primRec} already contains lemmas adapted to this kind of situation.

\begin{Coqanswer}
filter010IsPR :
forall g : nat -> nat, isPR 1 g -> isPR 3 (fun _ b _ : nat => g b)
\end{Coqanswer}

Thus, our first subgoal is solved easily and the proof ends, applying alreday proven lemmas.


\begin{Coqsrc}
 - unfold plus_alt; apply ind1ParamIsPR.
    + apply filter010IsPR, succIsPR.
    + apply idIsPR.
  - apply plus_alt_ok. 
Qed.
\end{Coqsrc}


\begin{todo}
Comment more examples from   \href{../theories/html/hydras.Ackermann.MoreAck.PrimRecExamples.html}{Ackermann.MoreAck.PrimRecExamples}.
\end{todo}


\begin{exercise}
There is a lot of lemmas similar to \texttt{filter010IsPR} in the \texttt{primRec} library, useful to control the arity of functions.
Thus, the reader may look at them, and invent simple examples of application for each one.
\end{exercise}

\begin{exercise}
Multiplication of natural number is already proven in the \texttt{primRec} library. Write a proof of your own, then compare to the library's version.
\end{exercise}

\subsubsection{More examples}

The following proof decomposes the \texttt{double} function as the composition of 
multiplication,with the pair composed of the identity and the constant function wihich returns $2$.
\emph{Note that the lemma \texttt{const1\_NIsPR} considers this function as an unary function (unlike \texttt{const0\_NIsPR} }. 

\begin{Coqsrc}
Lemma doubleIsPR : isPR 1 double.
Proof.
  unfold double; apply compose1_2IsPR.
  - apply idIsPR.
\end{Coqsrc}

\begin{Coqanswer}
subgoal 1 (ID 110) is:
 isPR 1 (fun _ : nat => 2)
subgoal 2 (ID 111) is:
 isPR 2 Init.Nat.mul
\end{Coqanswer}

 \begin{Coqsrc}
  - apply const1_NIsPR.
  - apply multIsPR.
Qed.
\end{Coqsrc}

\begin{exercise}
Consider the following functions:

\begin{Coqsrc}
Fixpoint fact n :=
  match n with 
          | 0 => 1
          | S p  => n * fact p
  end.

Fixpoint exp n p :=
  match p with
  | 0 => 1
  | S m =>  exp n m * n
  end.

Fixpoint tower2 n :=
  match n with
  | 0 => 1
  | S p => exp 2 (tower2 p)
  end.
\end{Coqsrc}

Prove they are primitive recursive.

\textbf{Hint:} You may have to look again at the lemmas of the library
\href{../theories/html/hydras.Ackermann.primRec.html}{Ackermann.primRec} if you meet some difficulty.

\emph{You may start this exercise with the file
\url{../exercises/primrec/MorePRExamples.v}.}
\end{exercise}







\index{primrec}{Exercises}

\begin{exercise}
Write a simple and readable proof that the Fibonacci function is primitive recursive.


\begin{Coqsrc}
Fixpoint fib (n:nat) : nat :=
  match n with
  | 0 => 1
  | 1 => 1
  | S ((S p) as q) => fib q + fib p
  end.
\end{Coqsrc}

\textbf{Hint:}  You may use as a helper the function which computes the pair 
$(\texttt{fib}(n+1),\texttt{fib}(n))$. 
Library \href{../theories/html/hydras.Ackermann.cPair.html}{Ackermann.cPair} contains
the definition of the encoding of $\mathbb{N}^2$ into $\mathbb{N}$, and the proofs that 
the associated constructor and projections are primitive recursive.

\emph{You may start this exercise with
\url{../exercises/primrec/FibonacciPR.v}.}

\end{exercise}

\index{primrec}{Exercises}
\begin{exercise}
Show that the function \texttt{min: naryFunc\,2} is primitive
recursive.

\emph{You may start this exercise with
\url{../exercises/primrec/MinPR.v}.}

\end{exercise}


\section{Proving that a given function is \emph{not} primitive recursive}

The best known example of a total recursive function which is not primitive recursive is the Ackermann function. We show how to adapt the classic proof (see for instance~\cite{planetmath}) to the constraints of \gallina. We hope this formal proof 
 is a nice opportunity to explore
the treatment of primitive recursive functions by R. O'Connor,
and to play with dependent types.

\subsection{The Ackermann function}

Here is a possible definition, where (\texttt{iterate\,$f$\,$n$})
is the $n$-th iteration of $f$.

\vspace{4pt}
\noindent
\emph{From \href{../theories/html/hydras.Prelude.Iterates.html}{Prelude.Iterates}}.

\begin{Coqsrc}
Fixpoint iterate {A:Type}(f : A -> A) (n: nat)(x:A) :=
  match n with
  | 0 => x
  | S p => f (iterate  f p x)
  end.
\end{Coqsrc}

\vspace{4pt}
\noindent
\emph{From \href{../theories/html/hydras.Ackermann.MoreAck.Ack.html}{Ackermann.MoreAck.Ack}}.
\index{maths}{Ackermann function}

\begin{Coqsrc}
Fixpoint Ack (m:nat) : nat -> nat :=
  match m with
  | 0 => S
  | S n => fun k =>  iterate (Ack n) (S k) 1
  end.
\end{Coqsrc}
 
This function is also known under the form of three equations, which we derive immediately from our definition.


\begin{Coqsrc}
Lemma Ack_0 : Ack 0 = S.
Proof refl_equal.

Lemma Ack_S_0 m : Ack (S m) 0 = Ack m 1. 
Proof.  now cbn. Qed.

Lemma Ack_S_S : forall m p,
    Ack (S m) (S p) = Ack m (Ack (S m) p).
Proof.  now cbn. Qed.
\end{Coqsrc}

The order of growth of the Ackermann function w.r.t. its first argument is illustrated by the following equalities.

\begin{Coqsrc}
Lemma Ack_1_n n: Ack 1 n = S  (S n).

Lemma Ack_2_n n: Ack 2 n = 2 * n + 3.

Lemma Ack_3_n n: Ack 3 n = exp2 (S (S (S n))) - 3.

Lemma Ack_4_n n: Ack 4 n = hyper_exp2 (S (S (S n))) - 3.
\end{Coqsrc}

Please note that for any given $n$, the unary function
(\texttt{Ack\,$n$}) is primitive recursive.

\emph{From \href{../theories/html/hydras.Ackermann.MoreAck.AckNotPR.html}{Ackermann.MoreAck.AckNotPR}}.
\begin{Coqsrc}
Theorem Ackn_IsPR (n: nat) : isPR 1 (Ack n).
\end{Coqsrc}

\subsection{A proof by induction on all primitive recursive functions}

Ìn order to prove that \texttt{Ack} (considered as a function of two arguments) is not primitive recursive, the usual method consists in two steps:


\begin{enumerate}
\item Prove that for any primitive recursive function $f:\mathbb{N}\rightarrow\mathbb{N}\rightarrow\mathbb{N}$, there exists some natural number $n$ depending on $f$, such that, for any $x$ and $y$, 
$f\,x\,y \leq \texttt{Ack}\,n\,(\textrm{max}\,x\,y)$ (we say that $f$ is \emph{``majorized''}  by \texttt{Ack}).
\item Show that \texttt{Ack} fails to satisfy this property.
\end{enumerate}

First, we prove that any primitive function of two arguments is majorized.
If we look at the inductive definition of primitive recursive functions,\pageref{def:Primrec}, it is obvious that a proof by induction on the construction of primitive recursive functions must consider functions of any arity.

\vspace{4pt}
\noindent
\emph{From \href{../theories/html/hydras.Ackermann.primRec.html}{Ackermann.primRec}.}

\begin{Coqsrc}
Scheme PrimRec_PrimRecs_ind := Induction for PrimRec
  Sort Prop
  with PrimRecs_PrimRec_ind := Induction for PrimRecs 
  Sort Prop.
\end{Coqsrc}

\begin{Coqanswer}
PrimRec_PrimRecs_ind :
forall (P : forall n : nat, PrimRec n -> Prop)
  (P0 : forall n n0 : nat, PrimRecs n n0 -> Prop),
(* successor *)
P 1 succFunc ->

(* zero *)
P 0 zeroFunc ->

* projections *)
(forall (n m : nat) (l : m < n), P n (projFunc n m l)) ->

(* composition *) 
(forall (n m : nat) (g : PrimRecs n m),
      P0 n m g -> forall h : PrimRec m, P m h -> 
      P n (composeFunc n m g h)) ->

(* primitive recursion *)
(forall (n : nat) (g : PrimRec n),
 P n g ->
    forall h : PrimRec (S (S n)), P (S (S n)) h -> 
     P (S n) (primRecFunc n g h)) ->

(* empty list of functions *)
(forall n : nat, P0 n 0 (PRnil n)) ->

(* add a function to a list *)
(forall (n m : nat) (p : PrimRec n),
   P n p -> 
   forall p0 : PrimRecs n m, P0 n m p0 -> 
   P0 n (S m) (PRcons n m p p0)) ->

(* conclusion ! *)
forall (n : nat) (p : PrimRec n), P n p
\end{Coqanswer}

For instance, proving a property shared by any primitive recursive functions of arity 2 leads to consider the case where that function is obtained by composition with a function of any 
arity $m$. The same problem happens with primitive 
recursion, where a function of arity $n$ is built out of a function of arity $n+1$ and a function of arity $n-1$.

Thus the lemma we will have to prove is the following one:


  \begin{quote}
    For any $n$, and any primitive recursive function $f$ of  arity $n$, there exists some natural number $q$ such that the following inequality holds:
 \[
  \forall x_1,\dots,x_n, 
      f(x_1,\dots,\,x_n)\leq\textrm{Ack}(q,\textrm{max}(x_1,\dots,x_n))
\]
 \end{quote}


But dots don't belong to \gallina's syntax! So, we may use \coq's vectors for denoting arbitrary tuples.

First, we extend \texttt{max} to vectors of natural numbers (using the notations of module \texttt{VectorNotations} and some more definitions from 
\href{../theories/html/hydras.Prelude.MoreVectors.html}{Prelude.MoreVectors}). So, \texttt{t\,$A$\,$n$} is the type of vectors of $n$ elements of type $A$, and the constants \texttt{cons}, \texttt{nil}, \texttt{map}, etc., refer to vectors and not to lists. Likewise, the notation \texttt{x::v} is an abbreviation for
\texttt{VectorDef.cons x ?n v}.


\begin{Coqsrc}
Fixpoint max_v {n:nat} : forall (v: Vector.t nat n) , nat :=
  match n as n0 return (Vector.t nat n0 -> nat)
  with
    0 => fun v => 0
  | S p => fun (v : Vector.t nat (S p)) =>
             (max (Vector.hd v) (max_v  (Vector.tl v)))
  end. 

Lemma max_v_2 : forall x y,  max_v (x::y::nil) = max x y.

Lemma max_v_lub : forall n (v: t nat n) y,
    (Forall (fun x =>  x <= y) v) -> max_v v <= y.

Lemma max_v_ge : forall n (v: t nat n) y,  In  y  v -> y <= max_v v.
\end{Coqsrc}


We have also to convert any application
$(f\,x_1\,x_2\,\dots\,x_n)$ into an application of a function 
to a single argument: the vector of all the $x_i$s.
This is already done in 
Library~\href{../theories/html/hydras.Ackermann.primRec.html}{Ackermann.primRec}.


\begin{Coqsrc}
Fixpoint evalList (m : nat) (l : Vector.t nat m) {struct l} :
 naryFunc m -> nat :=
  match l in (Vector.t _ m) return (naryFunc m -> nat) with
  | Vector.nil => fun x : naryFunc 0 => x
  | Vector.cons a n l' => fun x : naryFunc (S n) => evalList n l' (x a)
  end.
\end{Coqsrc}

Indeed, (\texttt{evalList $m$ $v$ $f$}) is the application to the vector $v$ of
an uncurried version of $f$.

In Library\href{../theories/html/hydras.Ackermann.MoreAck.AckNotPR.html}{Ackermann.MoreAck.AckNotPR}, we introduce a lighter notation.

\begin{Coqsrc}
(**  uncurried apply:
 
[v_apply f (x1::x2:: ... ::xn::nil)]  is [f x1 x2 ... xn] 
 *)

Notation "'v_apply' f v" := (evalList _ v f) ( at level 10, f at level 9).

Example Ex2 : forall (f: naryFunc 2) x y,
    v_apply f (x::y::nil) = f x y.
Proof.   intros; now cbn. Qed.

Example Ex4 : forall (f: naryFunc 4) x y z t,
    v_apply f (x::y::z::t::nil) = f x y z t.
Proof.  intros; now cbn. Qed.
\end{Coqsrc}

We are now able to translate in \gallina{} the notion of ``majorization'':

\begin{Coqsrc}
Definition majorized {n} (f: naryFunc n) (A: naryFunc 2) : Prop :=
  exists (q:nat), forall (v: t nat n),
      v_apply f v <= A q  (max_v v).

Definition majorizedPR {n} (x: PrimRec n) A := 
           majorized (evalPrimRec n x) A.

(** For vectors of functions *)

Definition majorizedS {n m} (fs : Vector.t (naryFunc n) m)
           (A : naryFunc 2):=
  exists N, forall (v: t nat n),
      max_v (map (fun f => v_apply f v) fs) <= A N (max_v v).

Definition majorizedSPR {n m} (x : PrimRecs n m) :=
  majorizedS (evalPrimRecs _ _ x).
\end{Coqsrc}

Now, it remains to prove that any primitive function is majorized by \texttt{Ack}.
The three base cases of construction of primitive recursive function are as follows:

\begin{Coqsrc}
Lemma majorSucc : majorizedPR  succFunc Ack.

Lemma majorZero : majorizedPR  zeroFunc Ack.

Lemma majorProjection (n m:nat)(H: m < n):
  majorizedPR (projFunc n m H) Ack.
\end{Coqsrc}


The rest of the cases are proved within a mutual  induction.


\begin{Coqsrc}
Lemma majorAnyPR:  forall n (x: PrimRec n),  majorizedPR  x Ack.
Proof.
  intros n x; induction x using PrimRec_PrimRecs_ind with
                  (P0 := fun n m y => majorizedSPR  y Ack).
  - apply majorSucc.
  - apply majorZero.
  - apply majorProjection. 
  \end{Coqsrc}

  \begin{Coqsrc}
  - (** function composition *)
\end{Coqsrc}
\begin{Coqanswer}
1 subgoal (ID 265)

  n, m : nat
  g : PrimRecs n m
  x : PrimRec m
  IHx : majorizedSPR g Ack
  IHx0 : majorizedPR x Ack
  ============================
  majorizedPR (composeFunc n m g x) Ack
\end{Coqanswer}

\begin{Coqsrc}
  ...
 - (** primitive recursion *)
\end{Coqsrc}

\begin{Coqanswer}
1 subgoal (ID 265)
  
  n : nat
  x1 : PrimRec n
  x2 : PrimRec (S (S n))
  IHx1 : majorizedPR x1 Ack
  IHx2 : majorizedPR x2 Ack
  ============================
  majorizedPR (primRecFunc n x1 x2) Ack
\end{Coqanswer}

\begin{Coqsrc}
 assert (L1 : forall i (v: t nat n) ,
               v_apply f (i::v)  <= Ack q (i + max_v v)).
    { induction i.
      ...
    }
    ...
-  (** empty list of functions *)
\end{Coqsrc}

\begin{Coqanswer}
1 subgoal (ID 266)
  
  n : nat
  ============================
  majorizedSPR (PRnil n) Ack
\end{Coqanswer}

\begin{Coqsrc}
  ...
- (** non-empty list of functions *)
\end{Coqsrc}

\begin{Coqanswer}
1 subgoal (ID 273)
  
  n, m : nat
  x : PrimRec n
  p : PrimRecs n m
  IHx : majorizedPR x Ack
  IHx0 : majorizedSPR p Ack
  ============================
  majorizedSPR (PRcons n m x p) Ack
\end{Coqanswer}
  
\begin{Coqsrc}
 ...
Qed.
\end{Coqsrc}

\subsection{Looking for a contradiction}

The following lemma is just a specialization of \texttt{majorAnyPR} to
binary functions (forgetting vectors, coming back to usual notations).

\begin{Coqsrc}
Lemma majorPR2 (f: naryFunc 2)(Hf : isPR 2 f)
  : exists (n:nat), forall x y,  f x y <= Ack n (max x  y).
\end{Coqsrc}

Then, our impossibility proof is just a sequence of easy small steps.

\begin{Coqsrc}
Section Impossibility_Proof.

  Hypothesis HAck : isPR 2 Ack.

  Fact Ack_majorized : majorized (Ack: naryFunc 2)  Ack.
  
  Section Contrad.
    Variable n: nat.
    Hypothesis  Hn: forall x y,  Ack x y <= Ack n (max x  y).

    Remark R01: n <> 0.
  
    Remark R02: n <> 1.
   
    Remark R03: max 2 n = n.
   
    Remark R04: Ack n (2 * n + 3) = Ack n (Ack 2 n).
       
    Remark R05: Ack n (2 * n + 3) <= Ack (n + 2) n.
   
    Remark R06: Ack (n + 2) n <= Ack n (n + 2).
   
    Remark R07: Ack n (n + 2) < Ack n (2 * n + 3).
   
    Lemma contrad : False.
   
  End Contrad.

  Lemma Ack_not_PR : False.

End Impossibility_Proof.
\end{Coqsrc}


%%% ICI






% \item Show that for all $n$ and $m$, $A\,n\,m > m$ and  that the functions $A\,n$ are strictly monotonous.

% Show also that, for any $n$ and $m$, $A\,n\,m \leq A\,(n+1)\,m$. 
%          Finally, show that for any $n$, $m$ and $k$, 
% $A\,k\,(A\,m\,n)\leq A\,(2+\textrm{max}(k,m))\,n$.
% \emph{\textbf{Done.}}
% \item Show that for any primitive recursive function $f$ of $k$ arguments,
% for any $x_1,\dots\,x_k$, $f(x_1,\dots,x_k)\leq A\,n\,(x_1+\dots+x_k)$. \emph{\textbf{Still to do.}}
% \item Show that the Ackermann function is not primitive recursive. \emph{\textbf{Still to do.}}
      
% \end{enumerate}


\begin{remark}
Note that the Ackermann function is a counter-example to the (false) statement:
\begin{quote}
{\color{red}
  ``Let $f$ be a function of type \texttt{naryFunc\,2}. If, for any $n$, the fonction $f(n)$ is primitive recursive, then f is primitive recursive.''}
\end{quote}
\end{remark}




